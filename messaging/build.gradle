plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.google.protobuf' version '0.8.15'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            proto.srcDirs += 'src/main/protos'
            // this is a hack to get Android Studio to recognize the generated protocol buffer classes
            java.srcDirs += 'build/generated/source/proto/main/java'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
        pickFirst 'META-INF/library_release.kotlin_module'
        pickFirst 'META-INF/lib_release.kotlin_module'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-collections-immutable-jvm:0.3.3'

    implementation("com.squareup.okhttp3:okhttp:4.9.1")
    implementation "ch.qos.logback:logback-classic:1.2.3"

    implementation 'io.github.microutils:kotlin-logging:1.12.5'

    api 'com.google.protobuf:protobuf-javalite:3.10.0'

    implementation 'com.android.support:exifinterface:28.0.0'
    implementation 'com.j256.simplemagic:simplemagic:1.16'

    api 'com.github.getlantern:db-android:a066a76a3f'
    implementation "com.github.getlantern.curve25519-java:curve25519-java:cb0f93fdc1"
    implementation 'com.github.getlantern.libsignal-protocol-java:signal-protocol-java:9d99c989c1'
    implementation 'com.github.getlantern:libsignal-metadata-java:77a7a914d4'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'org.jetbrains.kotlin:kotlin-test'
    androidTestImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3'

    androidTestImplementation 'net.sourceforge.streamsupport:android-retrofuture:1.7.3'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.10.0'
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}